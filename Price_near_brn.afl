_SECTION_BEGIN("price_action_nearbrn");

ticker = Name ();
curint = Interval(2);
tn = TimeNum();
startTime = 90000; // start in HHMMSS format
endTime = 93000;  // end in HHMMSS format
timeOK = tn >= startTime AND tn <= endTime;

if (LastValue(timeOK))
{
	//Get Previous Day data
	pDayO = TimeFrameGetPrice("O", inDaily, -1);	// yesterdays open
	pDayH = TimeFrameGetPrice("H", inDaily, -1);	// yesterdays high
	pDayL = TimeFrameGetPrice("L", inDaily, -1);	// yesterdays low
	pDayC = TimeFrameGetPrice("C", inDaily, -1);	// yesterdays close

	//Get Today's Data
	DayO = TimeFrameGetPrice("O", inDaily, 0);	//	day open
	DayH = TimeFrameGetPrice("H", inDaily, 0);	//	day high
	DayL = TimeFrameGetPrice("L", inDaily, 0);	//	day low

	//Calculation of BRN and RN
	lastC = round (Ref(C, 0));
	brn = int ((lastC+51)/100)*100;
	rn = IIf(brn - 50 > 0, brn-50, 0);
	 
	nbrn = brn+100;
	nrn = nbrn - 50;

	n1brn = IIf(brn-100 > 0, brn-100, 0);
	n1rn = IIf(n1brn-50 > 0, n1brn-50, 0);

	//Near BRN findings
	C_brn =  abs  (lastC - brn)/(lastC)*100;
	C_nbrn = abs  (lastC - nbrn)/(lastC)*100;
	C_n1brn = abs (lastC - n1brn)/(lastC)*100;

	C_near_brn = IIf (C_brn < 2, 2, 0);   //if less than 2% then near brn = true
	C_near_nbrn = IIf (C_nbrn < 2, 2, 0);
	C_near_n1brn = IIf (C_n1brn < 2, 2, 0);

	//Near RN findings
	C_rn =  abs  (lastC - rn)/(lastC)*100;
	C_nrn = abs  (lastC - nrn)/(lastC)*100;
	C_n1rn = abs (lastC - n1rn)/(lastC)*100;

	C_near_rn   = IIf (C_rn < 2, 1, 0);  //if less than 2% then near rn = true
	C_near_nrn  = IIf (C_nrn < 2, 1, 0);
	C_near_n1rn = IIf (C_n1rn < 2, 1, 0);

	//previous close near brn findings
	pDayC_brn =  ((pDayC - brn)/(pDayC))*100;       
	pDayC_nbrn = ((pDayC - nbrn)/(pDayC))*100;
	pDayC_n1brn = ((pDayC - n1brn)/(pDayC))*100;  

	pDayC_near_brn = IIf (abs(pDayC_brn) < 2.0, 2, 0);

	//previous close near rn findings
	pDayC_rn =  (((pDayC - rn)/(pDayC))*100);  
	pDayC_near_rn = IIf (abs(pDayC_rn) < 1.0, 1, 0);

	//Monthly and Weekly resistances
	TimeFrameSet( inDaily ); // switch now to Daily
	H_1 = Ref(H,-1);
	L_1 = Ref(L,-1);
	m_res_comp = HHV(H_1, 30);
	m_sup_comp = LLV(L_1, 30);
	w_res_comp = HHV(H_1, 7);
	w_sup_comp = LLV(L_1, 7);
	TimeFrameRestore(); // restore time frame to original

	m_res = TimeFrameExpand(m_res_comp, inDaily);
	m_sup = TimeFrameExpand(m_sup_comp, inDaily);
	w_res = TimeFrameExpand(w_res_comp, inDaily);
	w_sup = TimeFrameExpand(w_sup_comp, inDaily);

	m_res_buy_strength  =  IIf (( O>m_res), 2, 0) + IIf ((C>m_res), 4, 0);
	m_sup_sell_strength =  IIf (( O<m_sup), 2, 0) + IIf ((C>m_sup), 4, 0);

	w_res_buy_strength  = IIf  ((O>w_res), 1, 0) + IIf  ((c>w_res), 2, 0);
	w_sup_sell_strength = IIf  ((O<w_sup), 1, 0) + IIf  ((C<w_sup), 2, 0);

	//4HR - Peaks
	TimeFrameSet( in1Minute*240 ); // switch now to Daily
	hr4_peak_comp = Peak (C, 2.4);
	hr4_trough_comp = trough (C, 2.4);
	TimeFrameRestore(); // restore time frame to original

	hr4_peak = TimeFrameExpand (hr4_peak_comp,in1Minute*240);
	hr4_trough = TimeFrameExpand (hr4_trough_comp, in1Minute*240);

	hr4_peak_buy_strength = IIf   ((O>hr4_peak), 2, 0) + IIf ((C>hr4_peak), 2, 0);
	hr4_trough_buy_strength = IIf (( O>hr4_trough), 1, 0) + IIf ((C>hr4_trough), 1, 0);

	hr4_peak_sell_strength = IIf   ((O<hr4_peak), 1, 0) + IIf ((C<hr4_peak), 1, 0);
	hr4_trough_sell_strength = IIf (( O<hr4_trough), 2, 0) + IIf ((C<hr4_trough), 2, 0);

	//30m - Peaks
	TimeFrameSet( in1Minute*30 ); // switch now to Daily
	m30_peak_comp = Peak (C, 0.7);
	m30_trough_comp = trough (C, 0.7);
	TimeFrameRestore(); // restore time frame to original

	m30_peak = TimeFrameExpand (m30_peak_comp,in1Minute*30);
	m30_trough = TimeFrameExpand (m30_trough_comp, in1Minute*30);

	m30_peak_buy_strength = IIf   ((O>m30_peak), 2, 0) + IIf ((C>m30_peak), 2, 0);
	m30_trough_buy_strength = IIf (( O>m30_trough), 1, 0) + IIf ((C>m30_trough), 1, 0);

	m30_peak_sell_strength = IIf   ((O<m30_peak), 2, 0) + IIf ((C<m30_peak), 2, 0);
	m30_trough_sell_strength = IIf (( O<m30_trough), 1, 0) + IIf ((C<m30_trough), 1, 0);

	/*
	TimeFrameSet( in1Minute*30 ); // switch now to 30min
	adx30m = (ADX()/25);
	ADX_acc30m = adx30m - Ref(adx30m, -1);
	adx30m_buy_strength_30m = (IIf(int (PDI()/MDI()) > 0, ((int(ADX()/25))) + (ADX() > Ref(ADX(),-1)), 0));
	adx30m_sell_strength_30m = (IIf(int (MDI()/PDI()) > 0, ((int(ADX()/25))) + (ADX() > Ref(ADX(),-1)), 0));
	TimeFrameRestore(); // restore time frame to original

	adx30m_buy_strength  = TimeFrameExpand(adx30m_buy_strength_30m, in1Minute*30);
	adx30m_sell_strength = TimeFrameExpand(adx30m_sell_strength_30m, in1Minute*30);

	//ADX_acc30m_exp = TimeFrameExpand(ADX_acc30m, in1Minute*30);
	//dist = (ADX_acc30m_exp)*abs(C-Ref(C,-1));
	*/

	gap_down_sell = (Ref(C,-1) >= pDayL) AND (C <= pDayL);
	gap_up_buy    = (Ref(C,-1) <= pDayH) AND (C >= pDayH);

	C_pdc = abs(C-pDayC)/pDayC*100;
	pDayMid = (pDayO+pDayH+pDayL+pDayC)/4;

	c_day_buy_strength1 =  Cross(C, pDayH) + Cross(C, pDayC) + gap_up_buy    + (C >= dayO) + (C > pDayMid);
	c_day_sell_strength1 = Cross(pDayL, C) + Cross(pDayC, C) + gap_down_sell + (C <= dayO) + (C < pDayMid);

	c_day_buy_strength2  =  (DayO > pDayMid)+2*(DayO > pDayH)+(DayO > pDayC)+(DayH > pDayH)+(C > dayO)+2*(C > pDayH) + 
							(C > pDayMid) + (C_pdc < 1);
	c_day_sell_strength2 =  (DayO < pDayMid)+2*(DayO < pDayL)+(DayO < pDayC)+(DayL < pDayL)+(C < dayO)+(C < pDayL) + 
							(C < pDayMid) + (C_pdc < 1);

	c_day_buy_strength = c_day_buy_strength1 + c_day_buy_strength2;
	c_day_sell_strength = c_day_sell_strength1 + c_day_sell_strength2;


	//Previous day strengths
	pdayc_buy_strength1 = IIf (abs(pDayC-pDayH) < abs (pDayC-pDayL), 1, 0); // if close is near high
	pdayc_sell_strength1 = IIf (abs(pDayC-pDayL) < abs (pDayC-pDayH), 1, 0);

	pdayc_buy_strength2 =  2*(pDayC_near_brn) + (pDayC_brn>0);
	pdayc_sell_strength2 = 2*(pDayC_near_brn) + (pDayC_brn<0);

	pdayc_buy_strength3 =  2*(pDayC > pDayO);
	pdayc_sell_strength3 = 2*(pDayC < pDayO);

	pdayc_buy_strength = pdayc_buy_strength1 + pdayc_buy_strength2 + pdayc_buy_strength3;
	pdayc_sell_strength = pdayc_sell_strength1 + pdayc_sell_strength2 + pdayc_sell_strength3;

	EMA_c15 = EMA (C,15);
	EMA_c25 = EMA (C,25);

	trend_buy_strength =   (C > EMA_C15) + (EMA_c15 > EMA_c25) + (EMA_c15 > Ref(EMA_c15,-1));
	trend_sell_strength =  (C < EMA_C15) + (EMA_c15 < EMA_c25) + (EMA_c15 < Ref(EMA_c15,-1));

	//from hourlybo
	C_1 = Ref (C, -1);
	H_1 = Ref (H, -1);
	L_1 = Ref (L, -1);
	hourly_bo_buy_strength 	= 	(C > HHV(C_1,12)) + (C > HHV (H_1,12)) + (ATR(1) > EMA(ATR(1),8));
	hourly_bo_sell_strength =  	(C < LLV(C_1,12)) + (C < LLV (L_1,12)) + (ATR(1) > EMA(ATR(1),8));


	//Location Strength and Weakness
	//If the green candle starts at the 5 bbb more likely it would go up.
	//If the red candle starts at the   5 bbt, more likely it would come down.

	TimeFrameSet( inHourly * 4); // switch now to 240 mins
	bbt_240m_comp = BBandTop( c, 15, 2 );
	bbb_240m_comp = BBandBot( c, 15, 2 );
	TimeFrameRestore(); // restore time frame to original

	TimeFrameSet( in1Minute * 30); // switch now to hourly
	bbt_30m_comp = BBandTop( c, 15, 2 );
	bbb_30m_comp = BBandBot( c, 15, 2 );
	TimeFrameRestore(); // restore time frame to original

	bbt_5m = BBandTop( c, 15, 2);
	bbb_5m = BBandBot( c, 15, 2);
	bbt_240m = TimeFrameExpand(bbt_240m_comp, inHourly*4);
	bbb_240m = TimeFrameExpand(bbb_240m_comp, inHourly*4);
	bbt_30m = TimeFrameExpand(bbt_30m_comp, in1Minute*30);
	bbb_30m = TimeFrameExpand(bbb_30m_comp, in1Minute*30);

	bb_5m_buyweakness  = (abs(C-bbt_5m)/C*100 < 0.5) + (abs(C-bbt_240m)/C*100 < 0.5) + (abs(C-bbt_30m)/C*100 < 0.5);
	bb_5m_sellweakness = (abs(C-bbb_5m)/C*100 < 0.5) + (abs(C-bbb_240m)/C*100 < 0.5) + (abs(C-bbb_30m)/C*100 < 0.5);

	/*
	//Potential BO calculation near brn
	*/

	TimeFrameSet( in1Minute*240 ); // switch now to 240min
	consol_abv_brn_comp = (LLV(C,7) > brn );
	consol_blw_brn_comp = (HHV(C,7) < brn );
	//LLV_array_comp = Ref(LLV(L,11),-1);
	//HHV_array_comp = Ref(HHV(H,11),-1);
	TimeFrameRestore(); // restore time frame to original

	consol_abv_brn_buy_strength = TimeFrameExpand(consol_abv_brn_comp, in1Minute*240)*2;
	consol_blw_brn_sell_strength = TimeFrameExpand(consol_blw_brn_comp, in1Minute*240)*2;

	buy_strength_tmp = consol_abv_brn_buy_strength+pdayc_buy_strength + c_day_buy_strength + trend_buy_strength+hourly_bo_buy_strength - bb_5m_buyweakness; 
	sell_strength_tmp = consol_blw_brn_sell_strength+pdayc_sell_strength + c_day_sell_strength + trend_sell_strength+hourly_bo_sell_strength - bb_5m_sellweakness;

	buy_strength1  = IIf(buy_strength_tmp  > sell_strength_tmp, buy_strength_tmp-sell_strength_tmp, 0);
	sell_strength1 = IIf(sell_strength_tmp > buy_strength_tmp, sell_strength_tmp-buy_strength_tmp, 0);

	//Qualifiers
	//First candle strength
	//candle close - how far from high
	candle_buy_strength1 =  IIf((abs(C-H)/C)*100 < 0.1, 1, -1);
	candle_sell_strength1 = IIf((abs(C-L)/C)*100 < 0.1, 1, -1);

	candle_body = (abs(C-O)/((H-L)+0.01)*100) < 2;
	candle_buy_strength2 =  IIF ((C>O) AND (candle_body), 1, -1);
	candle_sell_strength2 =  IIf((C<O) AND (candle_body), 1, -1);

	candle_buy_strength = candle_buy_strength1 + candle_buy_strength2;
	candle_sell_strength = candle_sell_strength1 + candle_sell_strength2;

	//How far from the nearest resistance  
	// if previous 30m closing price is atleast 1 % below the 30m bbt -- good
	// if previous 4h  closing price is atleast 1 % below the 4hr bbt -- good
	// if previous 30m closing price is atleast 1 % above the 30m bbb -- good
	// if previous 4h  closing price is atleast 1 % above the 4hr bbb -- good

	TimeFrameSet( in1Minute*30 ); // 
	bbt30m_sd15_comp = BBandTop( C, 15, 2 );
	bbb30m_sd15_comp = BBandBot( C, 15, 2 );
	TimeFrameRestore(); // 

	TimeFrameSet( in1Minute*240 ); //
	bbt240m_sd15_comp = BBandTop( C, 15, 2 );
	bbb240m_sd15_comp = BBandBot( C, 15, 2 );
	TimeFrameRestore(); // 

	bbt30m_sd15 =  Ref(TimeFrameExpand(bbt30m_sd15_comp, in1Minute*30), -1);
	bbb30m_sd15 =  Ref(TimeFrameExpand(bbb30m_sd15_comp, in1Minute*30), -1);
	bbt240m_sd15 = Ref(TimeFrameExpand(bbt240m_sd15_comp, in1Minute*240), -1);
	bbb240m_sd15 = Ref(TimeFrameExpand(bbb240m_sd15_comp, in1Minute*240), -1);

	C_belowbbt30m = IIf((buy_strength1 > sell_strength1), IIf((C<bbt30m_sd15), (bbt30m_sd15 - C)/C*100 > 0.75, 2), 0);
	C_belowbbt240m = IIf((buy_strength1 > sell_strength1), IIf((C<bbt240m_sd15), (bbt240m_sd15 - C)/C*100 > 0.75, 2), 0);

	C_abovebbb30m = IIf((sell_strength1 > buy_strength1),  IIf((C>bbb30m_sd15),  (C - bbb30m_sd15 )/C*100 > 0.75, 2), 0);
	C_abovebbb240m = IIf((sell_strength1 > buy_strength1), IIf((C>bbb240m_sd15), (C - bbt240m_sd15)/C*100 > 0.75, 2), 0);

	dist_res_Buy_strength = C_belowbbt30m + C_belowbbt240m;
	dist_sup_Sell_strength = C_abovebbb30m + C_abovebbb240m;

	Qualifier1_buy_strength  = candle_buy_strength  + dist_res_Buy_strength;
	Qualifier1_sell_strength = candle_sell_strength + dist_sup_Sell_strength;

	buy_strength2 = IIf(buy_strength1  > sell_strength1,  buy_strength1 + Qualifier1_buy_strength, 0);
	sell_strength2 = IIf(sell_strength1  > buy_strength1,  sell_strength1 + Qualifier1_sell_strength, 0);

	//Second level Qualifiers
	//if already price has gone up or down > 2%, it might slow down a bit (unless it is big gap up

	C_pDayC = abs(C-pDayC)/pDayC * 100 < 1;

	Qualifier2_buy_strength = IIf ((buy_strength2 > 9) AND  (buy_strength2 < 13) AND (C_pDayC), 1, 0);
	Qualifier2_sell_strength = IIf ((sell_strength2 > 9) AND  (sell_strength2 < 13) AND (C_pDayC), 1, 0);


	buy_strength3 = buy_strength2 + Qualifier2_buy_strength;
	sell_strength3 = sell_strength2 + Qualifier2_sell_strength;

	buy_strength  = round (buy_strength3);
	sell_strength = round (Sell_strength3);

	Filter = (buy_strength >8) OR  (sell_strength >8);

	SetOption("NoDefaultColumns", True );
	AddTextColumn( Name(),     "Symbol",1.0, colorDefault,colorDefault, 80);
	//AddColumn( DateTime(), "Date / Time", formatDateTime, colorDefault,colorDefault, 70 );//
	//AddColumn(C_near_brn, "brn", 1.2);
	//AddColumn(C_near_nbrn, "nbrn", 1.2);
	//AddColumn(C_near_n1brn, "n1brn", 1.2);
	AddColumn(buy_strength, "buy_s", 1.0);
	AddColumn(sell_strength, "sell_s", 1.0);
	AddColumn(C, "CMP", 1.2);
	AddColumn((C-pDayC)/pDayC*100, "%change", 1.2);
	AddColumn( DateTime(), "Date / Time", formatDateTime, colorDefault,colorDefault, 70 );//

	AlertIf(Filter, "","nbrn "+NumToStr(C,8.2,True,True)+" "+NumToStr(buy_strength,8.0,True,True)+" "+NumToStr(sell_strength,8.0,True,True));
}
_SECTION_END();